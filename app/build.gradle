/*
 * Project:  Simple Collector
 * Purpose:  Mobile application for simple data collection.
 * Author:   NikitaFeodonit, nfeodonit@yandex.com
 * ****************************************************************************
 * Copyright (c) 2017-2018 NextGIS, info@nextgis.com
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser Public License for more details.
 *
 * You should have received a copy of the GNU Lesser Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
//    buildToolsVersion '28.0.3'

    // Setup the Sentry DSN from the file 'sentry.properties', which should contain
    // sentry.dsn=https://publicKey:secretKey@host:port/1?options
    // DO NOT PUBLIC the file 'sentry.properties' !!!
    File sentryPropFile = rootProject.file('sentry.properties')
    String sentryDsn = ""
    if (sentryPropFile.exists()) {
        Properties sentryProp = new Properties()
        sentryProp.load(sentryPropFile.newDataInputStream())
        if (sentryProp.containsKey("sentry.dsn")) {
            sentryDsn = sentryProp.get("sentry.dsn")
        }
    } else {
        throw new GradleException("File 'sentry.properties' in the project root is not found.")
    }
    println("sentryDsn == ${sentryDsn}")
    if (sentryDsn == "") {
        throw new GradleException("Property 'sentry.dsn' in file 'sentry.properties' is not set.")
    }

    defaultConfig {
        applicationId "com.nextgis.simple_collector"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 10
        versionName "1.0"
        archivesBaseName = "simple-collector-${versionName}-rev.${versionCode}"
        buildConfigField "String", "SENTRY_DSN", "\"${sentryDsn}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':maplibui')
    implementation 'io.sentry:sentry-android:1.6.8'
    implementation 'com.google.android.gms:play-services-analytics:16.0.6'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
